IO::Compress::Lzma(3pm)                                                             User Contributed Perl Documentation                                                             IO::Compress::Lzma(3pm)

NAME
       IO::Compress::Lzma - Write lzma files/buffers

SYNOPSIS
           use IO::Compress::Lzma qw(lzma $LzmaError) ;

           my $status = lzma $input => $output [,OPTS]
               or die "lzma failed: $LzmaError\n";

           my $z = IO::Compress::Lzma->new( $output [,OPTS] )
               or die "lzma failed: $LzmaError\n";

           $z->print($string);
           $z->printf($format, $string);
           $z->write($string);
           $z->syswrite($string [, $length, $offset]);
           $z->flush();
           $z->tell();
           $z->eof();
           $z->seek($position, $whence);
           $z->binmode();
           $z->fileno();
           $z->opened();
           $z->autoflush();
           $z->input_line_number();
           $z->newStream( [OPTS] );

           $z->close() ;

           $LzmaError ;

           # IO::File mode

           print $z $string;
           printf $z $format, $string;
           tell $z
           eof $z
           seek $z, $position, $whence
           binmode $z
           fileno $z
           close $z ;

DESCRIPTION
       This module provides a Perl interface that allows writing lzma compressed data to files or buffer.

       For reading lzma files/buffers, see the companion module IO::Uncompress::UnLzma.

Functional Interface
       A top-level function, "lzma", is provided to carry out "one-shot" compression between buffers and/or files. For finer control over the compression process, see the "OO Interface" section.

           use IO::Compress::Lzma qw(lzma $LzmaError) ;

           lzma $input_filename_or_reference => $output_filename_or_reference [,OPTS]
               or die "lzma failed: $LzmaError\n";

       The functional interface needs Perl5.005 or better.

   lzma $input_filename_or_reference => $output_filename_or_reference [, OPTS]
       "lzma" expects at least two parameters, $input_filename_or_reference and $output_filename_or_reference and zero or more optional parameters (see "Optional Parameters")

       The $input_filename_or_reference parameter

       The parameter, $input_filename_or_reference, is used to define the source of the uncompressed data.

       It can take one of the following forms:

       A filename
            If the $input_filename_or_reference parameter is a simple scalar, it is assumed to be a filename. This file will be opened for reading and the input data will be read from it.

       A filehandle
            If the $input_filename_or_reference parameter is a filehandle, the input data will be read from it.  The string '-' can be used as an alias for standard input.

       A scalar reference
            If $input_filename_or_reference is a scalar reference, the input data will be read from $$input_filename_or_reference.

       An array reference
            If $input_filename_or_reference is an array reference, each element in the array must be a filename.

            The input data will be read from each file in turn.

            The complete array will be walked to ensure that it only contains valid filenames before any data is compressed.

       An Input FileGlob string
            If  $input_filename_or_reference  is a string that is delimited by the characters "<" and ">" "lzma" will assume that it is an input fileglob string. The input is the list of files that match
            the fileglob.

            See File::GlobMapper for more details.

       If the $input_filename_or_reference parameter is any other type, "undef" will be returned.

       The $output_filename_or_reference parameter

       The parameter $output_filename_or_reference is used to control the destination of the compressed data. This parameter can take one of these forms.

       A filename
            If the $output_filename_or_reference parameter is a simple scalar, it is assumed to be a filename.  This file will be opened for writing and the compressed data will be written to it.

       A filehandle
            If the $output_filename_or_reference parameter is a filehandle, the compressed data will be written to it.  The string '-' can be used as an alias for standard output.

       A scalar reference
            If $output_filename_or_reference is a scalar reference, the compressed data will be stored in $$output_filename_or_reference.

       An Array Reference
            If $output_filename_or_reference is an array reference, the compressed data will be pushed onto the array.

       An Output FileGlob
            If $output_filename_or_reference is a string that is delimited by the characters "<" and ">" "lzma" will assume that it is an output fileglob string. The output is  the  list  of  files  that
            match the fileglob.

            When $output_filename_or_reference is an fileglob string, $input_filename_or_reference must also be a fileglob string. Anything else is an error.

            See File::GlobMapper for more details.

       If the $output_filename_or_reference parameter is any other type, "undef" will be returned.

   Notes
       When  $input_filename_or_reference  maps to multiple files/buffers and $output_filename_or_reference is a single file/buffer the input files/buffers will be stored in $output_filename_or_reference
       as a concatenated series of compressed data streams.

   Optional Parameters
       The optional parameters for the one-shot function "lzma" are (for the most part) identical to those used with the OO interface defined in the "Constructor  Options"  section.  The  exceptions  are
       listed below

       "AutoClose => 0|1"
            This option applies to any input or output data streams to "lzma" that are filehandles.

            If "AutoClose" is specified, and the value is true, it will result in all input and/or output filehandles being closed once "lzma" has completed.

            This parameter defaults to 0.

       "BinModeIn => 0|1"
            This option is now a no-op. All files will be read in binmode.

       "Append => 0|1"
            The behaviour of this option is dependent on the type of output data stream.

            •    A Buffer

                 If "Append" is enabled, all compressed data will be append to the end of the output buffer. Otherwise the output buffer will be cleared before any compressed data is written to it.

            •    A Filename

                 If "Append" is enabled, the file will be opened in append mode. Otherwise the contents of the file, if any, will be truncated before any compressed data is written to it.

            •    A Filehandle

                 If "Append" is enabled, the filehandle will be positioned to the end of the file via a call to "seek" before any compressed data is written to it.  Otherwise the file pointer will not be
                 moved.

            When "Append" is specified, and set to true, it will append all compressed data to the output data stream.

            So  when  the  output  is a filehandle it will carry out a seek to the eof before writing any compressed data. If the output is a filename, it will be opened for appending. If the output is a
            buffer, all compressed data will be appended to the existing buffer.

            Conversely when "Append" is not specified, or it is present and is set to false, it will operate as follows.

            When the output is a filename, it will truncate the contents of the file before writing any compressed data. If the output is a filehandle its position will not be changed. If the output is a
            buffer, it will be wiped before any compressed data is output.

            Defaults to 0.

   Oneshot Examples
       Here are a few example that show the capabilities of the module.

       Streaming

       This very simple command line example demonstrates the streaming capabilities of the module.  The code reads data from STDIN, compresses it, and writes the compressed data to STDOUT.

           $ echo hello world | perl -MIO::Compress::Lzma=lzma -e 'lzma \*STDIN => \*STDOUT' >output.lzma

       The special filename "-" can be used as a standin for both "\*STDIN" and "\*STDOUT", so the above can be rewritten as

           $ echo hello world | perl -MIO::Compress::Lzma=lzma -e 'lzma "-" => "-"' >output.lzma

       Compressing a file from the filesystem

       To read the contents of the file "file1.txt" and write the compressed data to the file "file1.txt.lzma".

           use strict ;
           use warnings ;
           use IO::Compress::Lzma qw(lzma $LzmaError) ;

           my $input = "file1.txt";
           lzma $input => "$input.lzma"
               or die "lzma failed: $LzmaError\n";

       Reading from a Filehandle and writing to an in-memory buffer

       To read from an existing Perl filehandle, $input, and write the compressed data to a buffer, $buffer.

           use strict ;
           use warnings ;
           use IO::Compress::Lzma qw(lzma $LzmaError) ;
           use IO::File ;

           my $input = IO::File->new( "<file1.txt" )
               or die "Cannot open 'file1.txt': $!\n" ;
           my $buffer ;
           lzma $input => \$buffer
               or die "lzma failed: $LzmaError\n";

       Compressing multiple files

       To compress all files in the directory "/my/home" that match "*.txt" and store the compressed data in the same directory

           use strict ;
           use warnings ;
           use IO::Compress::Lzma qw(lzma $LzmaError) ;

           lzma '</my/home/*.txt>' => '<*.lzma>'
               or die "lzma failed: $LzmaError\n";

       and if you want to compress each file one at a time, this will do the trick

           use strict ;
           use warnings ;
           use IO::Compress::Lzma qw(lzma $LzmaError) ;

           for my $input ( glob "/my/home/*.txt" )
           {
               my $output = "$input.lzma" ;
               lzma $input => $output
                   or die "Error compressing '$input': $LzmaError\n";
           }

OO Interface
   Constructor
       The format of the constructor for "IO::Compress::Lzma" is shown below

           my $z = IO::Compress::Lzma->new( $output [,OPTS] )
               or die "IO::Compress::Lzma failed: $LzmaError\n";

       The constructor takes one mandatory parameter, $output, defined below and zero or more "OPTS", defined in "Constructor Options".

       It returns an "IO::Compress::Lzma" object on success and "undef" on failure.  The variable $LzmaError will contain an error message on failure.

       If you are running Perl 5.005 or better the object, $z, returned from IO::Compress::Lzma can be used exactly like an IO::File filehandle.  This means that all normal output file operations can  be
       carried out with $z.  For example, to write to a compressed file/buffer you can use either of these forms

           $z->print("hello world\n");
           print $z "hello world\n";

       Below is a simple exaple of using the OO interface to create an output file "myfile.lzma" and write some data to it.

           my $filename = "myfile.lzma";
           my $z = IO::Compress::Lzma->new($filename)
               or die "IO::Compress::Lzma failed: $LzmaError\n";

           $z->print("abcde");
           $z->close();

       See the "Examples" for more.

       The mandatory parameter $output is used to control the destination of the compressed data. This parameter can take one of these forms.

       A filename
            If the $output parameter is a simple scalar, it is assumed to be a filename. This file will be opened for writing and the compressed data will be written to it.

       A filehandle
            If the $output parameter is a filehandle, the compressed data will be written to it.  The string '-' can be used as an alias for standard output.

       A scalar reference
            If $output is a scalar reference, the compressed data will be stored in $$output.

       If the $output parameter is any other type, "IO::Compress::Lzma"::new will return undef.

   Constructor Options
       "OPTS" is any combination of zero or more the following options:

       "AutoClose => 0|1"
            This  option is only valid when the $output parameter is a filehandle. If specified, and the value is true, it will result in the $output being closed once either the "close" method is called
            or the "IO::Compress::Lzma" object is destroyed.

            This parameter defaults to 0.

       "Append => 0|1"
            Opens $output in append mode.

            The behaviour of this option is dependent on the type of $output.

            •    A Buffer

                 If $output is a buffer and "Append" is enabled, all compressed data will be append to the end of $output. Otherwise $output will be cleared before any data is written to it.

            •    A Filename

                 If $output is a filename and "Append" is enabled, the file will be opened in append mode. Otherwise the contents of the file, if any, will be truncated  before  any  compressed  data  is
                 written to it.

            •    A Filehandle

                 If  $output is a filehandle, the file pointer will be positioned to the end of the file via a call to "seek" before any compressed data is written to it.  Otherwise the file pointer will
                 not be moved.

            This parameter defaults to 0.

       "Filter => $filter"
            When present  $filter  option must be an object of type "Lzma::Filter::Lzma1".  See "Lzma::Filter::Lzma" for a definition of "Lzma::Filter::Lzma1".

            If this option is not present an "Lzma::Filter::Lzma1" object with default values will be used.

       "Strict => 0|1"
            This is a placeholder option.

   Examples
       Streaming

       This very simple command line example demonstrates the streaming capabilities of the module. The code reads data from STDIN or all the files given on the commandline, compresses it, and writes the
       compressed data to STDOUT.

           use strict ;
           use warnings ;
           use IO::Compress::Lzma qw(lzma $LzmaError) ;

           my $z = IO::Compress::Lzma->new("-", Stream => 1)
               or die "IO::Compress::Lzma failed: $LzmaError\n";

           while (<>) {
               $z->print("abcde");
           }
           $z->close();

       Note the use of "-" to means "STDOUT". Alternatively you can use "\*STDOUT".

       Compressing a file from the filesystem

       To read the contents of the file "file1.txt" and write the compressed data to the file "file1.txt.lzma" there are a few options

       Start by creating the compression object and opening the input file

           use strict ;
           use warnings ;
           use IO::Compress::Lzma qw(lzma $LzmaError) ;

           my $input = "file1.txt";
           my $z = IO::Compress::Lzma->new("file1.txt.lzma")
               or die "IO::Compress::Lzma failed: $LzmaError\n";

           # open the input file
           open my $fh, "<", "file1.txt"
               or die "Cannot open file1.txt: $!\n";

           # loop through the input file & write to the compressed file
           while (<$fh>) {
               $z->print($_);
           }

           # not forgetting to close the compressed file
           $z->close();

Methods
   print
       Usage is

           $z->print($data)
           print $z $data

       Compresses and outputs the contents of the $data parameter. This has the same behaviour as the "print" built-in.

       Returns true if successful.

   printf
       Usage is

           $z->printf($format, $data)
           printf $z $format, $data

       Compresses and outputs the contents of the $data parameter.

       Returns true if successful.

   syswrite
       Usage is

           $z->syswrite $data
           $z->syswrite $data, $length
           $z->syswrite $data, $length, $offset

       Compresses and outputs the contents of the $data parameter.

       Returns the number of uncompressed bytes written, or "undef" if unsuccessful.

   write
       Usage is

           $z->write $data
           $z->write $data, $length
           $z->write $data, $length, $offset

       Compresses and outputs the contents of the $data parameter.

       Returns the number of uncompressed bytes written, or "undef" if unsuccessful.

   flush
       Usage is

           $z->flush;

       Flushes any pending compressed data to the output file/buffer.

       Returns true on success.

   tell
       Usage is

           $z->tell()
           tell $z

       Returns the uncompressed file offset.

   eof
       Usage is

           $z->eof();
           eof($z);

       Returns true if the "close" method has been called.

   seek
           $z->seek($position, $whence);
           seek($z, $position, $whence);

       Provides a sub-set of the "seek" functionality, with the restriction that it is only legal to seek forward in the output file/buffer.  It is a fatal error to attempt to seek backward.

       Empty parts of the file/buffer will have NULL (0x00) bytes written to them.

       The $whence parameter takes one the usual values, namely SEEK_SET, SEEK_CUR or SEEK_END.

       Returns 1 on success, 0 on failure.

   binmode
       Usage is

           $z->binmode
           binmode $z ;

       This is a noop provided for completeness.

   opened
           $z->opened()

       Returns true if the object currently refers to a opened file/buffer.

   autoflush
           my $prev = $z->autoflush()
           my $prev = $z->autoflush(EXPR)

       If the $z object is associated with a file or a filehandle, this method returns the current autoflush setting for the underlying filehandle. If "EXPR" is present, and is non-zero, it  will  enable
       flushing after every write/print operation.

       If $z is associated with a buffer, this method has no effect and always returns "undef".

       Note that the special variable $| cannot be used to set or retrieve the autoflush setting.

   input_line_number
           $z->input_line_number()
           $z->input_line_number(EXPR)

       This method always returns "undef" when compressing.

   fileno
           $z->fileno()
           fileno($z)

       If the $z object is associated with a file or a filehandle, "fileno" will return the underlying file descriptor. Once the "close" method is called "fileno" will return "undef".

       If the $z object is associated with a buffer, this method will return "undef".

   close
           $z->close() ;
           close $z ;

       Flushes any pending compressed data and then closes the output file/buffer.

       For  most  versions of Perl this method will be automatically invoked if the IO::Compress::Lzma object is destroyed (either explicitly or by the variable with the reference to the object going out
       of scope). The exceptions are Perl versions 5.005 through 5.00504 and 5.8.0. In these cases, the "close" method will be called automatically, but not until global destruction of all  live  objects
       when the program is terminating.

       Therefore, if you want your scripts to be able to run on all versions of Perl, you should call "close" explicitly and not rely on automatic closing.

       Returns true on success, otherwise 0.

       If the "AutoClose" option has been enabled when the IO::Compress::Lzma object was created, and the object is associated with a file, the underlying file will also be closed.

   newStream([OPTS])
       Usage is

           $z->newStream( [OPTS] )

       Closes the current compressed data stream and starts a new one.

       OPTS consists of any of the options that are available when creating the $z object.

       See the "Constructor Options" section for more details.

Importing
       No symbolic constants are required by IO::Compress::Lzma at present.

       :all Imports "lzma" and $LzmaError.  Same as doing this

                use IO::Compress::Lzma qw(lzma $LzmaError) ;

EXAMPLES
SUPPORT
       General feedback/questions/bug reports should be sent to <https://github.com/pmqs/IO-Compress-Lzma/issues> (preferred) or <https://rt.cpan.org/Public/Dist/Display.html?Name=IO-Compress-Lzma>.

SEE ALSO
       Compress::Zlib,   IO::Compress::Gzip,   IO::Uncompress::Gunzip,   IO::Compress::Deflate,   IO::Uncompress::Inflate,   IO::Compress::RawDeflate,   IO::Uncompress::RawInflate,   IO::Compress::Bzip2,
       IO::Uncompress::Bunzip2, IO::Uncompress::UnLzma, IO::Compress::Xz, IO::Uncompress::UnXz, IO::Compress::Lzip, IO::Uncompress::UnLzip, IO::Compress::Lzop, IO::Uncompress::UnLzop,  IO::Compress::Lzf,
       IO::Uncompress::UnLzf, IO::Compress::Zstd, IO::Uncompress::UnZstd, IO::Uncompress::AnyInflate, IO::Uncompress::AnyUncompress

       IO::Compress::FAQ

       File::GlobMapper, Archive::Zip, Archive::Tar, IO::Zlib

AUTHOR
       This module was written by Paul Marquess, "pmqs@cpan.org".

MODIFICATION HISTORY
       See the Changes file.

COPYRIGHT AND LICENSE
       Copyright (c) 2005-2024 Paul Marquess. All rights reserved.

       This program is free software; you can redistribute it and/or modify it under the same terms as Perl itself.

perl v5.38.2                                                                                     2024-09-02                                                                         IO::Compress::Lzma(3pm)
